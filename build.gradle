buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

apply plugin: 'com.bmuschko.nexus'

group = 'be.nelcea'
version = '0.0.3'
sourceCompatibility = '1.11'

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    implementation 'com.fasterxml.jackson.jr:jackson-jr-objects:2.10.3'
    implementation 'org.slf4j:slf4j-api:1.7.30'

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}

modifyPom {
    project {
        name 'unifi-controller-client'
        description 'Java library for communication with Ubiquiti UniFi controller'
        url 'https://github.com/nelcea/unifi-controller-client'
        inceptionYear '2020'

        scm {
            url 'https://github.com/nelcea/unifi-controller-client'
            connection 'scm:https://github.com/nelcea/unifi-controller-client.git'
            developerConnection 'scm:git@github.com:nelcea/unifi-controller-client.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'ebariaux'
                name 'Eric Bariaux'
                email 'ebariaux@nelcea.be'
            }
        }
    }
}

extraArchive {
    sources = true
    tests = false
    javadoc = true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/nelcea/unifi-controller-client")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }

}
